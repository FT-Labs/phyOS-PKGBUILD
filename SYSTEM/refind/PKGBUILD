# Maintainer: David Runge <dvzrv@archlinux.org>

pkgbase=refind
pkgname=(refind refind-docs)
pkgver=1.0
pkgrel=02
pkgdesc="An EFI boot manager"
arch=(any)
backup=(etc/default/refind)
url="https://www.github.com/FT-Labs/refind"
makedepends=(bash dosfstools efibootmgr gnu-efi)
source=(https://github.com/FT-Labs/refind/releases/download/$pkgver/$pkgbase-$pkgver.tar.gz)
sha256sums=('e765b7a37f8344486f536163183fa7adc83a01b75a4188fb39f17942c79ad66f')
_arch='x64'

prepare() {
  cd $srcdir
  # remove the path prefix from the css reference, so that the css can live
  # in the same directory
  sed -e 's|../Styles/||g' -i docs/$pkgbase/*.html
  # hardcode RefindDir, so that refind-install can find refind_x64.efi
  sed -e 's|RefindDir=\"\$ThisDir/refind\"|RefindDir="/usr/share/refind/"|g' -i refind-install
}

build() {
  make
  make gptsync
  # NOTE: build can not be parallelized
  make fs -j1
}

package_refind() {
  license=(BSD CCPL GPL2 GPL3 LGPL3)
  depends=(bash dosfstools efibootmgr phyOS-sysconf)
  optdepends=(
    'gptfdisk: for finding non-vfat ESP with refind-install'
    'imagemagick: for refind-mkfont'
    'openssl: for generating local certificates with refind-install'
    'python: for refind-mkdefault'
    'refind-docs: for HTML documentation'
    'sbsigntools: for EFI binary signing with refind-install'
    'sudo: for privilege elevation in refind-install and refind-mkdefault'
  )

  cd $srcdir
  # NOTE: the install target calls refind-install, therefore we install things
  # manually
  # efi binaries
  install -vDm 644 refind/*.efi -t "$pkgdir/usr/share/$pkgname/"
  install -vDm 644 drivers_*/*.efi -t "$pkgdir/usr/share/refind/drivers_$_arch/"
  install -vDm 644 gptsync/*.efi -t "$pkgdir/usr/share/$pkgname/tools_$_arch/"
  # sample config
  install -vDm 644 $pkgname.conf-sample -t "$pkgdir/usr/share/$pkgname/"
  install -vDm 644 $pkgname.default "$pkgdir/etc/default/$pkgbase"
  install -vDm 644 $pkgname.conf-sample "$pkgdir/etc/$pkgbase.d/$pkgbase.conf"
  # keys
  install -vDm 644 keys/*{cer,crt} -t "$pkgdir/usr/share/$pkgname/keys/"
  # keysdir
  install -vdm 700 "$pkgdir/etc/refind.d/keys"
  # fonts
  install -vDm 644 fonts/*.png -t "$pkgdir/usr/share/$pkgname/fonts/"
  # icons
  cp -rf icons "$pkgdir/usr/share/$pkgbase"
  cp -rf refind-auto "$pkgdir/usr/share/$pkgbase"
  install -vDm 755 refind-auto/refind-generate.service -t "$pkgdir/usr/lib/systemd/system/"
  # scripts
  install -vDm 755 {refind-{install,mkdefault,theme},refind-mkconfig,refind-mkconfig-efi,mvrefind} -t "$pkgdir/usr/bin/"
  install -vDm 755 fonts/mkfont.sh "$pkgdir/usr/bin/$pkgname-mkfont"
  # man pages
  install -vDm 644 docs/man/*.8 -t "$pkgdir/usr/share/man/man8/"
  # # docs
  # install -vDm 644 {CREDITS,NEWS,README}.txt -t "$pkgdir/usr/share/doc/$pkgname/"
  # install -vDm 644 fonts/README.txt "$pkgdir/usr/share/doc/$pkgname/README.$pkgname-mkfont.txt"
  # install -vDm 644 icons/README "$pkgdir/usr/share/doc/$pkgname/README.icons.txt"
  # install -vDm 644 keys/README.txt "$pkgdir/usr/share/doc/$pkgname/README.keys.txt"
  # license
  install -vDm 644 LICENSE.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}

package_refind-docs() {
  pkgdesc+=" - documentation"
  license=(FDL1.3)

  cd $srcdir
  install -vDm 644 docs/$pkgbase/*.{html,png,svg,txt} -t "$pkgdir/usr/share/doc/$pkgbase/html/"
  install -vDm 644 docs/Styles/*.css -t "$pkgdir/usr/share/doc/$pkgbase/html/"
  install -vDm 644 images/$pkgbase-banner.{png,svg} -t "$pkgdir/usr/share/doc/$pkgbase/html/"
}
